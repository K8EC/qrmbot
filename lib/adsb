#!/usr/bin/perl
#
# 2-clause BSD license.
# Copyright (c) 2019, 2020 molo1134@github. All rights reserved.

use strict;
use utf8;
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");
use Encode qw(decode);
use I18N::Langinfo qw(langinfo CODESET);
use JSON qw( decode_json );
use URI::Escape;
use Math::Round;
use POSIX qw(floor);
use POSIX qw(strftime);
use POSIX qw(tzset);
use Time::Piece ();

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;
use Location;
use Util;

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);

our $apikey = undef;

# change this to 0 to always exit with success
our $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq getEggdropUID();

my $callOrReg;
my $icaoQuery;
my $loc;

my $i = 0;
while ($i <= $#ARGV) {
  if ($ARGV[$i] =~ /--geo/) {
    $i++;
    next;
  }
  if ($ARGV[$i] =~ /^0x([A-F0-9]{6})$/i) {
    $icaoQuery = $1;
    #print "icaoQuery: $icaoQuery\n";
  } elsif ($ARGV[$i] =~ /^[A-Z0-9-]+$/) {
    $callOrReg = $ARGV[$i];
  } else {
    if (defined($loc)) {
      $loc = $loc . " " . $ARGV[$i];
    } else {
      $loc = $ARGV[$i];
    }
  }
  $i++;
}

updateICAOtypes();

my $geo;
my ($lat, $lon);
my $url;
#print "loc: $loc\n";
if (defined($icaoQuery)) {
  $url = "https://globe.adsb.fi/re-api/?find_hex=" . uri_escape($icaoQuery);
} elsif (defined($callOrReg)) {
  $url = "https://globe.adsb.fi/re-api/?find_reg=" . uri_escape($callOrReg);
} elsif (defined($loc) and $loc =~ /^-?\d+(.\d+)?,-?\d+(.\d+)?$/) {
  ($lat, $lon) = split /,/, $loc;
  $url = "https://globe.adsb.fi/re-api/?closest=" . uri_escape($lat) . "," .
    uri_escape($lon) . ",25"; # 25 nmi
} elsif (defined($loc)) {
  $loc =~ s/^\s*//;
  $loc =~ s/\s*$//;
  $geo = argToCoords($loc);
  ($lat, $lon) = split /,/, $geo;
  $url = "https://globe.adsb.fi/re-api/?closest=" . uri_escape($lat) . "," .
    uri_escape($lon) . ",25"; # 25 nmi
} else {
  print "usage: adsb <location>|<0xHEXCODE>|<callsign>|<registration>\n";
  exit $exitnonzeroonerror;
}

my $localgrid;
$localgrid = coordToGrid($lat, $lon) if defined($lat);

my $total = 0;

DOHTTP:

#print "$url\n";
open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
local $/;   # read entire file -- FIXME: potentially memory hungry
my $json = <HTTP>;
close(HTTP);
if (not $json =~ /^\s*{/) {
  print "error1 while retrieving data: from $url: $json\n";
  exit $exitnonzeroonerror;
}
my $j = decode_json($json);

$total = $j->{resultCount};

if ($total == 0 and defined($callOrReg) and $url =~ /find_reg/) {
  $url = "https://globe.adsb.fi/re-api/?find_callsign=" . uri_escape($callOrReg);
  goto DOHTTP;
}

print "no callsign/registration found: $callOrReg\n"
	if $total == 0 and $url =~ /find_callsign/;
print "icao code not found: $icaoQuery\n"
	if $total == 0 and $url =~ /find_hex/;
print "no planes found near $localgrid\n"
	if $total == 0 and $url =~ /closest/;
exit $exitnonzeroonerror if $total == 0;

show($j->{aircraft}->[0], $j->{now});

sub updateICAOtypes {
  my $url = "https://www4.icao.int/doc8643/External/AircraftTypes";
  my $typesfile = $ENV{'HOME'} . "/.icao-types";
  my $needICAO = 0;
  my $now = time;
  my $maxage=604800; # 1 week
  if (not -e $typesfile) {
    $needICAO = 1
  } else {
    my (undef, undef, undef, undef, undef, undef, undef, $size, undef, $mtime, undef, undef, undef) = stat $typesfile;
    if (defined $mtime) {
      my $age = $now - $mtime;
      if ($age > $maxage or $size == 0) {
	$needICAO = 1;
      }
    } else {
      $needICAO = 1;
    }
  }
  if ($needICAO == 1) {
    #print "$url\n";
    system("curl --stderr - -s -k -L --max-time 20 --retry 1 -o '$typesfile' -d '' '$url'");
  }
}

sub show
{
  my %rec = %{ (shift) };
  my $timestamp = shift;

  $rec{lat} = $rec{lastPosition}->{lat} if not defined $rec{lat} and defined $rec{lastPosition}->{lat};
  $rec{lon} = $rec{lastPosition}->{lon} if not defined $rec{lon} and defined $rec{lastPosition}->{lon};
  $rec{seen_pos} = $rec{lastPosition}->{seen_pos} if not defined $rec{seen_pos} and defined $rec{lastPosition}->{seen_pos};

  my $grid = coordToGrid($rec{lat}, $rec{lon});
  my $place = geolocate($rec{lat}, $rec{lon});
  my ($dist, $bearing) = distBearing($lat, $lon, $rec{lat}, $rec{lon});
  my $time = strftime("%H:%M:%Sz", gmtime(int($timestamp - $rec{seen_pos})));
  $time = strftime("%Y-%m-%d %H:%M:%Sz", gmtime(int($timestamp - $rec{seen_pos}))) if $rec{seen_pos} > 86400;

  my ($manuf, $model, $type, $etype, $icao, $owner);
  if ($rec{r} =~ m/^N/) {
    ($manuf, $model, $type, $etype, $icao, $owner) = getFaaReg($rec{r});
    #print("FAA reg: $manuf, $model, $type, $etype, $icao, $owner\n");
  }
  my ($airline, $orig, $dest, $fnum, $eta);
  if (defined($rec{flight}) and length($rec{flight}) > 0) {
    $rec{flight} =~ s/\s*$//;
    ($airline, $orig, $dest, $fnum, $eta) = getFlightTrack($rec{flight});
    #print("flight1: $airline, $orig, $dest, $fnum, $eta\n");
  } else {
    ($airline, $orig, $dest, $fnum, $eta) = getFlightTrack($rec{r});
    #print("flight2: $airline, $orig, $dest, $fnum, $eta\n");
  }
  #print join(",", ($airline, $orig, $dest)), "\n";
  #print join(",", ($manuf, $model, $type, $etype, $icao, $owner)), "\n";

  ################### formatted output

  if (defined($rec{flight}) and length($rec{flight}) > 0 and
      $rec{flight} ne $rec{r}) {
    print bold("$rec{flight}/$rec{r}");
  } else {
    print bold($rec{r});
  }
  if (not defined($model)) {
    print " ($rec{t})";
  } else {
    print " ($manuf $model)";
  }
  print " ($airline $fnum ", bold("$orig->$dest") if defined $airline;
  print " eta $eta" if defined $eta;
  print ")" if defined $airline;
  print " 0x$rec{hex}; ";
  print "squawk $rec{squawk} " if defined $rec{squawk} and length($rec{squawk}) > 0;
  print "alt $rec{alt_geom} ft;" if defined $rec{alt_geom};
  print "alt ~$rec{alt_baro} ft;" if defined $rec{alt_baro} and not defined $rec{alt_geom};
  printf (" track %.0f° %s; gnd speed %.0f knots", $rec{track}, azToNEWS($rec{track}), $rec{gs});
  print yellow(" MODE-S") if $rec{type} eq "mode_s";
  print yellow(" MLAT") if $rec{type} eq "mlat";
  print yellow(" TISB") if $rec{type} eq "tisb_icao";
  print green(" GND") if $rec{alt_baro} eq "ground" or $rec{alt_geom} eq "ground";
  if ($rec{alt_baro} eq "ground" or $rec{alt_geom} eq "ground" or $rec{alt_geom} <= 0 or $rec{gs} == 0) {
    print " at ";
  } else {
    print " over "
  }
  print "$place ($grid)";
  print " at $time";
  print "; owner: $owner" if defined $owner;
  printf (" -- %.1f km, %.0f° %s", $dist, $bearing, azToNEWS($bearing)) if defined($lat);
  print " from $localgrid" if defined($localgrid);

  if (defined($icao) and defined($rec{hex}) and lc $icao ne lc $rec{hex}) {
    print(" -- ", yellow("warning"));
    print(": observed ICAO mode S code $rec{hex} differs from registered code $icao");
  }

  print " -- https://globe.adsb.fi/?icao=$rec{hex}\n";
}

sub getFaaReg
{
  my $reg = shift;
  my ($manuf, $model, $type, $etype, $icao, $owner, $frac);
  $frac = 0;
  my $url = "https://registry.faa.gov/AircraftInquiry/Search/NNumberResult?nNumberTxt=$reg";
  #print "$url\n";
  open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
  while(<HTTP>) {
    #print;
    if (m|<td data-label="Manufacturer Name">([^<]+?)\s*</td>|) {
      $manuf = decodeEntities($1);
    }
    if (m|<td data-label="Model">([^<]+?)\s*</td>|) {
      $model = decodeEntities($1);
    }
    if (m|<td data-label="Aircraft Type">([^<]+?)\s*</td>|) {
      $type = decodeEntities($1);
    }
    if (m|<td data-label="Engine Type">([^<]+?)\s*</td>|) {
      $etype = decodeEntities($1);
    }
    if (m|<td data-label="Name" colspan="3">([^<]+?)\s*</td>|) {
      $owner = decodeEntities($1);
    }
    if (m|<td data-label="Mode S Code \(Base 16 / Hex\)">([0-9A-F]{6})</td>|) {
      $icao = $1;
    }
    $frac = 1 if m|<td data-label="Fractional Owner">YES</td>|;
  }
  close(HTTP);
  $owner .= " (fractional)" if $frac;
  return ($manuf, $model, $type, $etype, $icao, $owner);
}

sub getFlightTrack
{
  my $flight = shift;
  my $url = "https://flightaware.com/live/flight/$flight";
  my ($airline, $orig, $dest, $fnum, $eta);
  my $token;
  #print "$url\n";
  open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
  while(<HTTP>) {
    if (/name="origin".*content="([A-Z]+)"/) {
      $orig = $1;
    }
    if (/name="destination".*content="([A-Z]+)"/) {
      $dest = $1;
    }
    if (m|"shortName":"([^"]*)"|) {
      $airline = $1;
    }
    if (m|name="twitter:description" content="Track.*?#(\d+)\s+.*?"\s*/>|) {
      $fnum = $1;
    }
    # var trackpollGlobals = {"TOKEN":"dd1320656957446e91ec1b5b1b04d2ce5a7d0a670f899c2e--da74a34ace4ede66ab25308cec9e6b9338c28a99","INTERVAL":65,"SINGLE_FLIGHT":true,"USERTOKEN":"323564526"};
    if (m|trackpollGlobals.*"TOKEN":"([0-9a-f-]+)"|) {
      $token = $1;
      #print "token: $token\n";
    }
  }
  close(HTTP);

  if (defined($token)) {
    $url = "https://flightaware.com/ajax/trackpoll.rvt?token=$token&locale=en_US&summary=1";
    #print "$url\n";
    open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
    while(<HTTP>) {
      #print;
      if (m/"actual":\d+\},"landingTimes":\{"scheduled":\d+,"estimated":(\d+),"actual":null/) {
	my $estimated = $1;
	my $now = time();
	#print "estimated: $estimated\n";
	#print "now $now\n";
	if ($estimated > $now) {
	  my $m = (($estimated - $now) / 60) % 60;
	  my $h = floor((($estimated - $now) / 60) / 60);
	  $eta = "${h}h${m}m" if $h > 0;
	  $eta = "${m}m" if $h <= 0;
	  #print "$eta\n";
	}
      }
    }
    close(HTTP);
  }

  return ($airline, $orig, $dest, $fnum, $eta);
}
